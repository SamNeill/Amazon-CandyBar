buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.0'
    }
}

plugins {
    id 'maven-publish'
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.google.com' }
        maven { url 'https://developer.huawei.com/repo' }
    }

    configurations.all {
        resolutionStrategy {
            force 'org.jetbrains.kotlin:kotlin-stdlib:1.9.21'
            force 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.21'
            force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.21'
            force 'org.jetbrains.kotlin:kotlin-stdlib-common:1.9.21'
        }
    }

    rootProject.ext {
        def major = 3
        def minor = 20
        def patch = 3

        VersionCode = major * 10000 + minor * 100 + patch
        VersionName = "$major.$minor.$patch"

        MinSdk = 21
        TargetSdk = 35
        CompileSdk = 35
        BuildTools = "34.0.0"
        KotlinVersion = '1.9.21'
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_11
                    targetCompatibility JavaVersion.VERSION_11
                }
            }
        }
        
        tasks.withType(JavaCompile).configureEach {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}
